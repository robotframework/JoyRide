/*
 *   Copyright 2012 Nokia Siemens Networks Oyj
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
grammar org.robotframework.text.Space

hidden(COMMENT)

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate space "http://www.robotframework.org/text/Space"

Model: 
	(tables+=RobotTable NL)+;

RobotTable:
	SettingsTable | TestCaseTable | KeywordTable | VariablesTable;

SettingsTable:
	header=SettingsTableHeader
	(NL+ settings+=Setting)+;

SettingsTableHeader: '*'+ ' ' SETTINGS ' ' '*'+;

TestCaseTable:
	header=TestCaseTableHeader
	(NL+ tests+=TestCase)+;

TestCaseTableHeader: '*'+ ' ' TEST ' ' CASES ' ' '*'+;

KeywordTable:
	header=KeywordTableHeader
	(NL+ keywords+=Keyword)+;

KeywordTableHeader:	'*'+ ' ' KEYWORDS ' ' '*'+;

VariablesTable:
	header=VariablesTableHeader
	(NL+ variables+=Variable)+;

VariablesTableHeader: '*'+ ' ' VARIABLES ' ' '*'+;

Setting:
	(ValuesSetting | StepSetting);

TestCase:
	name=MacroName
	(NL+ SEPARATOR (settings+=TestCaseSetting | steps+=RobotStep))+;

Keyword:
	name=MacroName
	(NL+ SEPARATOR (settings+=KeywordSetting | steps+=RobotStep))+;

Variable:
	ScalarVariable | ListVariable
;

ScalarVariable:
	name=ScalarName SEPARATOR  value=RobotText
;

ListVariable:
	name=ListName SEPARATOR value=Values
;

TestCaseSetting:
	SetupSetting | TeardownSetting | TimeoutSetting | TagsTestSetting | TemplateTestSetting | DocumentationTestSetting;

DocumentationTestSetting:
	name=DocumentationMacroSettingName SEPARATOR value=Values;

TemplateTestSetting:
	name=TemplateTestSettingName (SEPARATOR value=Values)?
;

TagsTestSetting:
	name=TagsTestSettingName (SEPARATOR value=Values)?
;

TagsTestSettingName: '['TAGS']';
TemplateTestSettingName: '['TEMPLATE']';

KeywordSetting:
    setting=(ArgumentsSetting | TeardownSetting | TimeoutSetting | SomeKeywordSetting);

ArgumentsSetting:
	name=ArgumentsSettingName
	(SEPARATOR args+=ScalarName)*
	(SEPARATOR default_args+=ScalarName '=' defaults+=RobotText)*
	(SEPARATOR ListName)?;

ArgumentsSettingName: '[' ARGUMENTS ']';

TimeoutSetting:
	name=TimeoutSettingName (SEPARATOR value=Values)?;

TeardownSetting:
    name=TeardownSettingName (SEPARATOR step=ExecutingStep)?
;

TeardownSettingName: '['TEARDOWN']';

SetupSetting:
    name=SetupSettingName (SEPARATOR step=ExecutingStep)?;

SetupSettingName: '['SETUP']';

SomeKeywordSetting:
	name=(DocumentationMacroSettingName |
	ReturnKeywordSettingName) 
	SEPARATOR 
	value=Values;

TimeoutSettingName: '[' TIMEOUT ']';
DocumentationMacroSettingName: '['DOCUMENTATION']';
ReturnKeywordSettingName: '[' RETURN ']';

ValuesSetting:
	name=(ForceTags |
	DefaultTags |
	LIBRARY |
	RESOURCE |
	TestTemplate |
	TestTimeout |
	DOCUMENTATION) 
	SEPARATOR 
	value=Values;

ForceTags: FORCE' 'TAGS;
DefaultTags: DEFAULT' 'TAGS;
TestTemplate: TEST ' ' TEMPLATE;
TestTimeout: TEST ' ' TIMEOUT;

StepSetting:
	name=( SuiteSetup |
	SuiteTeardown |
	TestSetup |
	TestTeardown) 
	SEPARATOR 
	value=ExecutingStep;

SuiteSetup:    SUITE' 'SETUP;
SuiteTeardown: SUITE' 'TEARDOWN;
TestSetup:     TEST' 'SETUP;
TestTeardown:  TEST' 'TEARDOWN;

RobotStep:
    ExecutingStep | AssigningStep	
;

ExecutingStep:
	keyword=[Keyword|MacroName] (SEPARATOR values+=RobotText)*;

AssigningStep:
	(vars+=[Variable|ScalarName] SEPARATOR)* 
	(vars+=[Variable|ScalarName] | vars+=[Variable|ListName]) (' '? '=')? 
	SEPARATOR keyword=[Keyword|MacroName] 
	(SEPARATOR values+=RobotText)*
;

Values:
	val=RobotText (SEPARATOR others+=RobotText)*;

ScalarName:
	'$' '{' RobotTextInVariableName => '}';

ListName:
	'@' '{' RobotTextInVariableName => '}'
;

VariableName: ScalarName | ListName;

RobotText: RobotTextPart (' ' RobotTextPart)*;

RobotTextPart: (SpecialChar | '{' | TEXT | ReservedWord | VariableName)+;

RobotTextInVariableName: RobotTextPartInVariableName (' ' RobotTextPartInVariableName)*;
RobotTextPartInVariableName: RobotTextSubpartInVariableName+ |
	(RobotTextSubpartInVariableName* ('{' RobotTextSubpartInVariableName* '}' RobotTextSubpartInVariableName*)+)
;
RobotTextSubpartInVariableName: SpecialChar | TEXT | ReservedWord | '$' | '@';

MacroName: MacroNamePart (' ' MacroNamePart)*;
MacroNamePart: (TEXT | ReservedWord | '$' | '@')+;

ReservedWord: TEST | CASES | SETTINGS | 
	          KEYWORDS | VARIABLES | SUITE | SETUP |
	          TEARDOWN | TAGS | TIMEOUT | TEMPLATE |
	          DEFAULT | FORCE | DOCUMENTATION | RESOURCE |
	          LIBRARY | RETURN | ARGUMENTS
;

SpecialChar: '*'|'['|']'|'=';

terminal NL: ('\r'|'\n')+;

terminal TEST: 'Test';
terminal CASES: 'Cases';
terminal SETTINGS: 'Settings';
terminal KEYWORDS: 'Keywords';
terminal VARIABLES: 'Variables';
terminal SUITE: 'Suite';
terminal SETUP: 'Setup';
terminal TEARDOWN: 'Teardown';
terminal TAGS: 'Tags';
terminal TIMEOUT: 'Timeout';
terminal TEMPLATE: 'Template';
terminal DEFAULT: 'Default';
terminal FORCE: 'Force';
terminal DOCUMENTATION: 'Documentation';
terminal RESOURCE: 'Resource';
terminal LIBRARY: 'Library';
terminal RETURN: 'Return';
terminal ARGUMENTS: 'Arguments';

terminal TEXT: !('='|' '|'\r'|'\n'|'*'|'{'|'}'|'$'|'@'|'['|']')+;

terminal SEPARATOR: ' '' '+;

terminal COMMENT: ' '* '#' !('\r'|'\n')*;